subprojects {

	sourceCompatibility = 1.6
	group = 'com.github.hamsterksu'
	version = '1.2.1'
	
    apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'signing'

    repositories {
       mavenCentral()
    }

    dependencies {
    }
	/*
	sourceSets {
		main {
			java {
				srcDir 'src'
			}
			resources {
				srcDir 'res'
			}
		}
	}*/

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from 'build/docs/javadoc'
	}
 
	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}
	
	artifacts {
		archives jar
		archives javadocJar
		archives sourcesJar
	}
	
	signing {
		sign configurations.archives
	}

    jar {
        manifest.attributes provider: 'gradle'
    }
	
	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	 
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}
	 
				pom.project {
				   name 'android-aptlibs-gradle-plugin'
				   packaging 'jar'
				   description 'Easy way to add popular apt libs to your android project'
				   url 'https://github.com/hamsterksu/android-aptlibs-gradle-plugin'
	 
				   scm {
					   url 'scm:git@github.com:hamsterksu/android-aptlibs-gradle-plugin.git'
					   connection 'scm:git@github.com:hamsterksu/android-aptlibs-gradle-plugin.git'
					   developerConnection 'scm:git@github.com:hamsterksu/android-aptlibs-gradle-plugin.git'
				   }
	 
				   licenses {
					   license {
						   name 'The Apache Software License, Version 2.0'
						   url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						   distribution 'repo'
					   }
				   }
	 
				   developers {
					   developer {
						   id 'hamsterksu'
						   name 'Gennadiy Dubina'
					   }
					   
					   developer {
						   id 'TheHiddenDuck'
						   name ' Alexander Osmanov'
					   }
				   }
			   }
			}
		}
	}
}